#!/usr/bin/stap

############################################################
# Schedtimes.stp
# Author: Jason Baron <jbaron@redhat.com>
# profiles threads and displays their run times
# wait times, including i/o wait times.
# Has two modes. When no arguments are given it profiles all
# threads. Alternatively, you can pass -c "program name"
############################################################

//constants
global RUNNING=0, QUEUED=1, SLEEPING=2

global tgid_queued_time, tgid_sleep_time, tgid_io_wait_time, tgid_run_time, prev_poll_time
global pid_state, pid_names
global previous_timestamp
global io_wait_count
global io_wait_incremented

function get_iowait:long(queue:long)
{
  return @cast(queue,"rq","kernel")->nr_iowait->counter;
}

probe kernel.trace("sched_switch") {
  previous_pid = $prev->pid;
  pid_names[previous_pid] = execname();
  previous_tgid = $prev->tgid;
  next_pid = $next->pid;
  next_tgid = $next->tgid;
  if (previous_pid) {
    if (!([previous_pid] in pid_state)) {
      //use this state as entry into state machine
      previous_timestamp[previous_pid] = gettimeofday_us();
      if ($prev->state > 0) {
        pid_state[previous_pid] = SLEEPING;
      } else if ($prev->state == 0) {
        pid_state[previous_pid] = QUEUED;
      }
    } else if (pid_state[previous_pid] == RUNNING) {
      t = gettimeofday_us();  
      tgid_run_time[previous_tgid] += (t - previous_timestamp[previous_pid]);
      previous_timestamp[previous_pid] = t;
      if ($prev->state > 0) {
        if ((get_iowait($rq) - io_wait_count[previous_pid]) > 0)
          io_wait_incremented[previous_pid] = 1;  
        pid_state[previous_pid] = SLEEPING;
      } else if ($prev->state == 0) {
        pid_state[previous_pid] = QUEUED;
      }
    }
  }
  if (next_pid) {
    io_wait_count[next_pid] = get_iowait($rq);
    if (!([next_pid] in pid_state)) {
      //use this state as entry into state machine
      previous_timestamp[next_pid] =  gettimeofday_us();
      pid_state[next_pid] = RUNNING;
    } else if (pid_state[next_pid] == QUEUED) {
      t = gettimeofday_us();
      tgid_queued_time[next_tgid] += (t - previous_timestamp[next_pid]);
      previous_timestamp[next_pid] = t;
      pid_state[next_pid] = RUNNING;
    }
  }
}

probe kernel.trace("sched_wakeup") {
  wakeup_pid = $p->pid;
  wakeup_tgid = $p->tgid;
  pid_names[wakeup_pid] = execname();
  if ((!$success) && (pid_state[wakeup_pid] != SLEEPING)) next
  if (!wakeup_pid) next

  if (!([wakeup_pid] in pid_state)) {
    //use this state as entry into state machine
    previous_timestamp[wakeup_pid] =  gettimeofday_us();
    pid_state[wakeup_pid] = QUEUED;
  } else if (pid_state[wakeup_pid] == SLEEPING) {
    t = gettimeofday_us();
    tgid_sleep_time[wakeup_tgid] += (t - previous_timestamp[wakeup_pid]);
    if (io_wait_incremented[wakeup_pid] == 1) {
      tgid_io_wait_time[wakeup_tgid] += (t - previous_timestamp[wakeup_pid]);
      io_wait_incremented[wakeup_pid] = 0;
    }
    previous_timestamp[wakeup_pid] = t;
    pid_state[wakeup_pid] = QUEUED;
  } 
}

probe timer.ms(1000) {
# times are stored in microseconds
  t = gettimeofday_us();
  total_period = t - prev_poll_time;
  printf("%d\n", t);
  foreach (tgid in tgid_run_time) {
    if (pid_names[tgid] != "") {
      printf ("cpu usage(%%):%\s:%d.%d\n", pid_names[tgid], tgid_run_time[tgid]*100/total_period,(100000*tgid_run_time[tgid]/total_period)%1000);
      printf ("sleep(%%):%\s:%d.%d\n", pid_names[tgid], tgid_sleep_time[tgid]*100/total_period,(100000*tgid_sleep_time[tgid]/total_period)%1000);
      printf ("queued(%%):%\s:%d.%d\n", pid_names[tgid], tgid_queued_time[tgid]*100/total_period,(100000*tgid_queued_time[tgid]/total_period)%1000);
      printf ("io wait(%%):%\s:%d.%d\n", pid_names[tgid], tgid_io_wait_time[tgid]*100/total_period,(100000*tgid_io_wait_time[tgid]/total_period)%1000);
    }
  }
  delete tgid_run_time;
  delete tgid_sleep_time;
  delete tgid_queued_time;
  delete tgid_io_wait_time;
  delete pid_names;
  prev_poll_time = t;
  printf("\n");
}

//probe syscall.exit { printf("%s[%d] exiting\n", execname(), tid())

probe begin {
  printf("%d\n", gettimeofday_us());
  printf("CPU Usage\n"); # script name
  printf("cpu usage(%%):0:100:0\n"); # script name
  printf("sleep(%%):0:100:0\n"); # script name
  printf("queued(%%):0:100:0\n"); # script name
  printf("io wait(%%):0:100:0\n"); # script name

  printf("\n");
  prev_poll_time = gettimeofday_us();
}
